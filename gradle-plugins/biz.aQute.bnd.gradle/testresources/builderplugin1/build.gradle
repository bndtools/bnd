/*
 *
 */

import aQute.bnd.gradle.Bundle

plugins {
	id 'biz.aQute.bnd.builder'
}

ext.projectprop = 'prop.project'
group = 'test.bnd.gradle'
version = '1.0.0'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'commons-codec:commons-codec:1.5'
	implementation 'commons-lang:commons-lang:2.6'
	testImplementation 'junit:junit:4.9'
}

def jarTask = tasks.named('jar', Jar) {
	ext.taskprop = 'prop.task'
	manifest {
		attributes('Implementation-Title': base.archivesName,
		'Implementation-Version': '${project.version}',
		'-includeresource': '{${.}/bar.txt}',
		'-include': '${.}/other.bnd',
		'Override': 'This should be overridden by the bnd file'
		)
	}
	bundle.bnd = ['Override': 'This will be ignored since there is a bnd file for this task']
}

def bundleTask = tasks.register('bundle', Bundle) {
	description = 'Bundle'
	from sourceSets.test.output
	archiveClassifier = 'bundle'
	bundle {
		bnd = '''
-exportcontents: doubler.impl
-sources: true
My-Header: my-value
text: TEXT
Bundle-Name: ${project.group}:${task.archiveBaseName}-${task.archiveClassifier}
Project-Name: ${project.name}
Project-Dir: ${project.dir}
Project-Output: ${project.output}
Project-Sourcepath: ${project.sourcepath}
Project-Buildpath: ${project.buildpath}
'''
		bnd 'Here: ${.}'
		bnd '-includeresource.lib': 'commons-lang-2.6.jar;lib:=true'
		sourceSet = sourceSets.test
		classpath = configurations.compileClasspath
		classpath jarTask
	}
	archiveVersion = '1.1.0'
}

tasks.named('assemble') {
	dependsOn bundleTask
	doLast {
		// Test that we store the generated manifest back into the task
		println "### jar Bundle-SymbolicName: ${jarTask.get().manifest.effectiveManifest.attributes['Bundle-SymbolicName']}"
		println "### bundle Bundle-SymbolicName: ${bundleTask.get().manifest.effectiveManifest.attributes['Bundle-SymbolicName']}"
	}
}

artifacts {
	runtimeOnly bundleTask
}
