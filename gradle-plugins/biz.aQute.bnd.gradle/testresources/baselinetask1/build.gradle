/*
 *
 */

import aQute.bnd.gradle.Baseline

plugins {
	id 'biz.aQute.bnd.builder'
}

group = 'test'
version = '1.1.0'

repositories {
	maven {
		url = uri('repodir')
	}
	mavenCentral()
}

dependencies {
	implementation 'commons-codec:commons-codec:1.5'
	implementation 'commons-lang:commons-lang:2.6'
	testImplementation 'junit:junit:4.9'
}

def jarTask = tasks.named('jar', Jar) {
	manifest {
		attributes('Implementation-Title': base.archivesName,
		'Implementation-Version': project.version,
		'-includeresource': '{bar.txt}',
		'-include': 'other.bnd',
		'Override': 'This should be overridden by the bnd file'
		)
	}
}

tasks.named('baseline', Baseline) {
	description = 'Baseline'
	group = 'build'
	ignoreFailures = true
}

tasks.register('baselineDiffpackages', Baseline) {
	description = 'Baseline diffpackages'
	bundle = jarTask
	baseline = configurations.baseline
	diffpackages = ['!doubler']
	diffpackages.add('*')
	ignoreFailures = false
}

tasks.register('baselineDiffignore', Baseline) {
	description = 'Baseline diffignore'
	bundle = jarTask
	baseline = configurations.baseline
	diffignore.add('foo.txt')
	diffignore = ['OSGI-OPT']
	ignoreFailures = false
}

tasks.register('baselineSelf', Baseline) {
	description = 'Baseline Self'
	bundle = jarTask
	baseline = jarTask.flatMap { it.archiveFile }
	ignoreFailures = false
	baselineReportDirName = 'foo'
}
