/*
 *
 */

import aQute.bnd.gradle.Bundle

plugins {
	id 'biz.aQute.bnd.builder' apply false
	id 'java-library'
}

group = 'test.bnd.gradle'

repositories {
	mavenCentral()
}

ext {
	extraInstructions = provider {
		'''\
ZZ-Delivered: true
'''
	}
}

// Not a bundle.
def jarTask = tasks.named('jar', Jar) {
	manifest {
		attributes('XX-Signed': provider({true}))
	}
}

def bundleTask = tasks.register('bundle', Bundle) {
	description = 'Bundle'
	from jarTask.map { zipTree(it.archiveFile) }
	archiveClassifier = 'bundle'
	manifest {
		attributes('YY-Sealed': provider({true}))
	}

	bundle {
		bnd = jarTask.flatMap { jar ->
			jar.archiveFile.map { file ->
				"-include: jar:${file.asFile.toURI()}!/META-INF/MANIFEST.MF"
			}
		}

		bnd extraInstructions

		bnd '''
Bundle-Name: ${project.group}:${task.archiveBaseName}-${task.archiveClassifier}
'''
	}
}

tasks.named('assemble') {
	dependsOn bundleTask
}

artifacts {
	runtimeOnly bundleTask
}
