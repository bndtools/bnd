/*
 * Master Gradle build script
 *
 * Depends on bndPlugin property set by settings.gradle.
 * and bnd_* values from gradle.properties.
 */

import aQute.bnd.build.Workspace

/* Add bnd gradle plugin as a script dependency */
buildscript {
  dependencies {
    classpath bndPlugin
  }
}

/* Initialize the bnd workspace */
ext.bndWorkspace = Workspace.getWorkspace(rootDir, bnd_cnf)
if (bndWorkspace == null) {
  throw new GradleException("Unable to load workspace ${rootDir}/${bnd_cnf}")
}
/* Set this property in the workspace. It may have been set on the command line with -P */
bndWorkspace.setProperty('bnd_repourl', bnd_repourl)

ext.cnf = rootProject.project(bnd_cnf)

/* Configure the subprojects */
subprojects {
  def bndProject = bndWorkspace.getProject(name)
  if (bndProject != null) {
    plugins.apply 'biz.aQute.bnd'
    group bnd('groupid')
    version bnd('base.version')
    /* Turn off javadoc 8 overly pedantic lint checking */
    if (JavaVersion.current().isJava8Compatible()) {
      javadoc.options.addStringOption('Xdoclint:none', '-quiet') 
    }
  }
}

task wrapper(type: Wrapper) {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}
