/*
 * maven Gradle build script
 */

def localrepo = System.getProperty('maven.repo.local')
if (localrepo) {
  localrepo = relativePath(uri(gradle.startParameter.currentDir).resolve(localrepo))
}
def dist = parent.project(bnd_build)
def windows = System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')
def mvnw = rootProject.file(windows ? 'mvnw.cmd' : 'mvnw')

def deploy = tasks.register('deploy', Exec.class) {
  def releaserepo = uri(bnd('releaserepo', dist.file('bundles'))) /* Release repository. */
  dependsOn dist.tasks.named('releaseDependencies')
  if (windows) {
    executable 'cmd'
    args '/c', mvnw
  } else {
    executable mvnw
  }
  args '--batch-mode'
  args '--no-transfer-progress'
  if (logger.isDebugEnabled()) {
    args '--debug'
  }
  args '-Pdist'
  args "-Dreleaserepo=${releaserepo}"
  if (localrepo) {
    args "-Dmaven.repo.local=${localrepo}"
  }
  args 'deploy'
}

def deployJFrog = tasks.register('deployJFrog', Exec.class) {
  enabled !bnd('-releaserepo.jfrog', '').empty
  def settings = parent.project('cnf').file('ext/jfrog-settings.xml')
  def deployState = deploy.map { it.state }
  onlyIf {
    def state = deployState.get()
    state.didWork && (state.failure == null)
  }
  dependsOn deploy
  if (windows) {
    executable 'cmd'
    args '/c', mvnw
  } else {
    executable mvnw
  }
  args '--batch-mode'
  args '--no-transfer-progress'
  if (logger.isDebugEnabled()) {
    args '--debug'
  }
  args '-Pjfrog'
  args "--settings=${settings}"
  if (localrepo) {
    args "-Dmaven.repo.local=${localrepo}"
  }
  args 'deploy'
}

deploy.configure {
  finalizedBy deployJFrog
}

def mvnClean = tasks.register('mvnClean', Exec.class) {
 if (windows) {
    executable 'cmd'
    args '/c', mvnw
  } else {
    executable mvnw
  }
  args '--batch-mode'
  args '--no-transfer-progress'
  if (logger.isDebugEnabled()) {
    args '--debug'
  }
  if (localrepo) {
    args "-Dmaven.repo.local=${localrepo}"
  }
  args 'clean'
}

tasks.named('clean') {
  dependsOn mvnClean
}

def mvnTestCompile = tasks.register('test-compile', Exec.class) {
  dependsOn dist.tasks.named('releaseDependencies')
  if (windows) {
    executable 'cmd'
    args '/c', mvnw
  } else {
    executable mvnw
  }
  args '--batch-mode'
  args '--no-transfer-progress'
  if (logger.isDebugEnabled()) {
    args '--debug'
  }
  if (localrepo) {
    args "-Dmaven.repo.local=${localrepo}"
  }
  args 'test-compile'
}
