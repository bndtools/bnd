/*
 * Master Gradle initialization script
 */

import aQute.bnd.osgi.Constants

pluginManagement {
    plugins {
        id "com.gradle.enterprise" version "3.13.4"
    }
}

/* Add bnd gradle plugin as a script dependency */
buildscript {
    repositories {
        maven {
            name = "Bnd Snapshots"
            url = uri(bnd_snapshots)
        }
        maven {
            name = "Bnd Releases"
            url = uri(bnd_releases)
        }
        mavenCentral()
    }
    dependencies {
        classpath("biz.aQute.bnd:biz.aQute.bnd.gradle:$bnd_version") {
            version {
                require bnd_version
                prefer "latest.release"
            }
        }
    }
}

plugins {
    id "com.gradle.enterprise"
}

if (Boolean.parseBoolean(System.getenv("CI"))) {
    gradleEnterprise {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

rootProject.name = "bnd"

gradle.ext.bndWorkspaceConfigure = { workspace ->
    if ("git diff --no-ext-diff --quiet".execute().waitFor() == 0) {
        workspace.setProperty(Constants.TSTAMP, "git show --no-patch --format=%ct000".execute().text.trim())
    } else {
        workspace.setProperty(Constants.TSTAMP, Long.toString(System.currentTimeMillis()))
    }
}

apply plugin: "biz.aQute.bnd.workspace"

includeBuild("gradle-plugins")
