/*
 * Master Gradle initialization script
 */

import aQute.bnd.osgi.Constants

/* Add bnd gradle plugin as a script dependency */
buildscript {
	repositories {
		mavenCentral()
		maven {
			url = uri(bnd_repourl)
		}
		gradlePluginPortal()
	}
	dependencies {
		classpath(bnd_plugin)
		classpath("com.gradle:gradle-enterprise-gradle-plugin:3.6.3")
	}
	/* Since the files in the repository change with each build, we need to recheck for changes */
	configurations.classpath {
		resolutionStrategy {
			cacheChangingModulesFor(30, "minutes")
			cacheDynamicVersionsFor(30, "minutes")
		}
	}
	dependencies {
		components {
			all { ComponentMetadataDetails details ->
				details.changing = true
			}
		}
	}
	/* Add bnd gradle plugin to buildscript classpath of rootProject */
	var bndPlugin = files(configurations.classpath.files)
	gradle.rootProject {
		buildscript {
			dependencies {
				classpath(bndPlugin)
			}
		}
	}
}

rootProject.name = "bnd"

gradle.ext.bndWorkspaceConfigure = { workspace ->
	/*
	 * Compute the build time stamp. 
	 * If the git workspace is clean, the build time is the time of the head commit.
	 * If the git workspace is dirty, the build time is the current time.
	 */
	if ("git diff --no-ext-diff --quiet".execute().waitFor() == 0) {
		workspace.setProperty(Constants.TSTAMP, "git show --no-patch --format=%ct000".execute().text.trim())
	} else {
		workspace.setProperty(Constants.TSTAMP, Long.toString(System.currentTimeMillis()))
	}
}

apply plugin: "biz.aQute.bnd.workspace"
apply plugin: "com.gradle.enterprise"
