apply plugin: 'groovy'

repositories {
  mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')  {
        exclude module: 'groovy-all'
    }
}

if (JavaVersion.current().isJava9Compatible()) {
    tasks.withType(GroovyCompile) {
        groovyOptions.fork 'jvmArgs': java9options
    }
}

test {
    dependsOn jar
    def source = file('testresources')
    def target = file("${buildDir}/testresources")
    doFirst { // copy test resources into build dir
        project.delete(target)
        copy {
            from source
            into target
        }
    }
}

release {
  dependsOn groovydoc
}

classes {
    inputs.property('pluginClasspath', bnd('pluginClasspath'))
    File pluginClasspathFile = new File(compileJava.destinationDir, 'plugin-under-test-metadata.properties')
    outputs.file(pluginClasspathFile).withPropertyName('pluginClasspathFile')
    doLast {
        def pluginClasspath = project.files(new File(buildDir, "${project.name}.jar"), bnd('pluginClasspath').tokenize(File.pathSeparatorChar))
        Properties properties = new Properties()
        properties.setProperty('implementation-classpath', pluginClasspath.asPath)
        pluginClasspathFile.withOutputStream {
          properties.store(it, 'plugin-under-test-metadata')
        }
    }
}
